buildscript {
    dependencies {
        classpath 'com.github.dexman545:Outlet:1.3.10'
    }
}
plugins {
    id 'java-library'
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'maven-publish'
}
apply plugin: 'dex.plugins.outlet'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

outlet.allowSnapshotsForProject = false
outlet.mcVersionRange = project.minecraft_version_range

group = project.group
description = project.displayname
base.archivesName = project.name
version = '1.10.1'

loom {
    mixin.defaultRefmapName = 'presencefootsteps.mixin.refmap.json'
}
repositories {
    maven { name 'minelp'; url 'https://repo.minelittlepony-mod.com/maven/snapshot' }
    maven { name 'minelp-release'; url 'https://repo.minelittlepony-mod.com/maven/release' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modApi "net.fabricmc:fabric-loader:${project.loader_version}"

    modApi fabricApi.module("fabric-api-base", project.fabric_version)
    modApi fabricApi.module("fabric-lifecycle-events-v1", project.fabric_version)
    modApi fabricApi.module("fabric-key-binding-api-v1", project.fabric_version)
    modApi fabricApi.module("fabric-resource-loader-v0", project.fabric_version)

    modApi "com.minelittlepony:kirin:${project.kirin_version}"
    include "com.minelittlepony:kirin:${project.kirin_version}"
}

processResources {
    inputs.property "version", '1.10.1'

    filesMatching("fabric.mod.json") {
        expand "version": '1.10.1',
                "minecraftVersion": project.minecraft_version_range,
                "loaderVersion": ">=${project.loader_version}",
                "fabricVersion": ">=${project.fabric_version}",
                "kirinVersion": ">=${kirin_version}",
                "minelpVersion": ">=${project.minelp_version}"
    }

    from 'LICENSE'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}
